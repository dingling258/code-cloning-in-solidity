// File: @openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (proxy/utils/Initializable.sol)

pragma solidity ^0.8.20;

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * The initialization functions use a version number. Once a version number is used, it is consumed and cannot be
 * reused. This mechanism prevents re-execution of each "step" but allows the creation of new initialization steps in
 * case an upgrade adds a module that needs to be initialized.
 *
 * For example:
 *
 * [.hljs-theme-light.nopadding]
 * ```solidity
 * contract MyToken is ERC20Upgradeable {
 *     function initialize() initializer public {
 *         __ERC20_init("MyToken", "MTK");
 *     }
 * }
 *
 * contract MyTokenV2 is MyToken, ERC20PermitUpgradeable {
 *     function initializeV2() reinitializer(2) public {
 *         __ERC20Permit_init("MyToken");
 *     }
 * }
 * ```
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To prevent the implementation contract from being used, you should invoke
 * the {_disableInitializers} function in the constructor to automatically lock it when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() {
 *     _disableInitializers();
 * }
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Storage of the initializable contract.
     *
     * It's implemented on a custom ERC-7201 namespace to reduce the risk of storage collisions
     * when using with upgradeable contracts.
     *
     * @custom:storage-location erc7201:openzeppelin.storage.Initializable
     */
    struct InitializableStorage {
        /**
         * @dev Indicates that the contract has been initialized.
         */
        uint64 _initialized;
        /**
         * @dev Indicates that the contract is in the process of being initialized.
         */
        bool _initializing;
    }

    // keccak256(abi.encode(uint256(keccak256("openzeppelin.storage.Initializable")) - 1)) & ~bytes32(uint256(0xff))
    bytes32 private constant INITIALIZABLE_STORAGE = 0xf0c57e16840df040f15088dc2f81fe391c3923bec73e23a9662efc9c229c6a00;

    /**
     * @dev The contract is already initialized.
     */
    error InvalidInitialization();

    /**
     * @dev The contract is not initializing.
     */
    error NotInitializing();

    /**
     * @dev Triggered when the contract has been initialized or reinitialized.
     */
    event Initialized(uint64 version);

    /**
     * @dev A modifier that defines a protected initializer function that can be invoked at most once. In its scope,
     * `onlyInitializing` functions can be used to initialize parent contracts.
     *
     * Similar to `reinitializer(1)`, except that in the context of a constructor an `initializer` may be invoked any
     * number of times. This behavior in the constructor can be useful during testing and is not expected to be used in
     * production.
     *
     * Emits an {Initialized} event.
     */
    modifier initializer() {
        // solhint-disable-next-line var-name-mixedcase
        InitializableStorage storage $ = _getInitializableStorage();

        // Cache values to avoid duplicated sloads
        bool isTopLevelCall = !$._initializing;
        uint64 initialized = $._initialized;

        // Allowed calls:
        // - initialSetup: the contract is not in the initializing state and no previous version was
        //                 initialized
        // - construction: the contract is initialized at version 1 (no reininitialization) and the
        //                 current contract is just being deployed
        bool initialSetup = initialized == 0 && isTopLevelCall;
        bool construction = initialized == 1 && address(this).code.length == 0;

        if (!initialSetup && !construction) {
            revert InvalidInitialization();
        }
        $._initialized = 1;
        if (isTopLevelCall) {
            $._initializing = true;
        }
        _;
        if (isTopLevelCall) {
            $._initializing = false;
            emit Initialized(1);
        }
    }

    /**
     * @dev A modifier that defines a protected reinitializer function that can be invoked at most once, and only if the
     * contract hasn't been initialized to a greater version before. In its scope, `onlyInitializing` functions can be
     * used to initialize parent contracts.
     *
     * A reinitializer may be used after the original initialization step. This is essential to configure modules that
     * are added through upgrades and that require initialization.
     *
     * When `version` is 1, this modifier is similar to `initializer`, except that functions marked with `reinitializer`
     * cannot be nested. If one is invoked in the context of another, execution will revert.
     *
     * Note that versions can jump in increments greater than 1; this implies that if multiple reinitializers coexist in
     * a contract, executing them in the right order is up to the developer or operator.
     *
     * WARNING: Setting the version to 2**64 - 1 will prevent any future reinitialization.
     *
     * Emits an {Initialized} event.
     */
    modifier reinitializer(uint64 version) {
        // solhint-disable-next-line var-name-mixedcase
        InitializableStorage storage $ = _getInitializableStorage();

        if ($._initializing || $._initialized >= version) {
            revert InvalidInitialization();
        }
        $._initialized = version;
        $._initializing = true;
        _;
        $._initializing = false;
        emit Initialized(version);
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} and {reinitializer} modifiers, directly or indirectly.
     */
    modifier onlyInitializing() {
        _checkInitializing();
        _;
    }

    /**
     * @dev Reverts if the contract is not in an initializing state. See {onlyInitializing}.
     */
    function _checkInitializing() internal view virtual {
        if (!_isInitializing()) {
            revert NotInitializing();
        }
    }

    /**
     * @dev Locks the contract, preventing any future reinitialization. This cannot be part of an initializer call.
     * Calling this in the constructor of a contract will prevent that contract from being initialized or reinitialized
     * to any version. It is recommended to use this to lock implementation contracts that are designed to be called
     * through proxies.
     *
     * Emits an {Initialized} event the first time it is successfully executed.
     */
    function _disableInitializers() internal virtual {
        // solhint-disable-next-line var-name-mixedcase
        InitializableStorage storage $ = _getInitializableStorage();

        if ($._initializing) {
            revert InvalidInitialization();
        }
        if ($._initialized != type(uint64).max) {
            $._initialized = type(uint64).max;
            emit Initialized(type(uint64).max);
        }
    }

    /**
     * @dev Returns the highest version that has been initialized. See {reinitializer}.
     */
    function _getInitializedVersion() internal view returns (uint64) {
        return _getInitializableStorage()._initialized;
    }

    /**
     * @dev Returns `true` if the contract is currently initializing. See {onlyInitializing}.
     */
    function _isInitializing() internal view returns (bool) {
        return _getInitializableStorage()._initializing;
    }

    /**
     * @dev Returns a pointer to the storage namespace.
     */
    // solhint-disable-next-line var-name-mixedcase
    function _getInitializableStorage() private pure returns (InitializableStorage storage $) {
        assembly {
            $.slot := INITIALIZABLE_STORAGE
        }
    }
}

pragma solidity ^0.8.20;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal onlyInitializing {
    }

    function __Context_init_unchained() internal onlyInitializing {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    function _contextSuffixLength() internal view virtual returns (uint256) {
        return 0;
    }
}

pragma solidity ^0.8.20;


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * The initial owner is set to the address provided by the deployer. This can
 * later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    /// @custom:storage-location erc7201:openzeppelin.storage.Ownable
    struct OwnableStorage {
        address _owner;
    }

    // keccak256(abi.encode(uint256(keccak256("openzeppelin.storage.Ownable")) - 1)) & ~bytes32(uint256(0xff))
    bytes32 private constant OwnableStorageLocation = 0x9016d09d72d40fdae2fd8ceac6b6234c7706214fd39c1cd1e609a0528c199300;

    function _getOwnableStorage() private pure returns (OwnableStorage storage $) {
        assembly {
            $.slot := OwnableStorageLocation
        }
    }

    /**
     * @dev The caller account is not authorized to perform an operation.
     */
    error OwnableUnauthorizedAccount(address account);

    /**
     * @dev The owner is not a valid owner account. (eg. `address(0)`)
     */
    error OwnableInvalidOwner(address owner);

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the address provided by the deployer as the initial owner.
     */
    function __Ownable_init(address initialOwner) internal onlyInitializing {
        __Ownable_init_unchained(initialOwner);
    }

    function __Ownable_init_unchained(address initialOwner) internal onlyInitializing {
        if (initialOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(initialOwner);
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        OwnableStorage storage $ = _getOwnableStorage();
        return $._owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        if (owner() != _msgSender()) {
            revert OwnableUnauthorizedAccount(_msgSender());
        }
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        if (newOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        OwnableStorage storage $ = _getOwnableStorage();
        address oldOwner = $._owner;
        $._owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// File: contracts/CCFBridgeV2.sol

pragma solidity ^0.8.20;


// Interface for the XAUT token
interface IERC20Token {
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
    function transfer(address to, uint256 amount) external returns (bool);
}

// Cross-chain Foundation Bridge
//v1.1 Added tokenWhitelist
contract CCFBridgeV2 is Initializable, OwnableUpgradeable {
    // Mapping to store minimum fees for each blockchain destination by token address
    mapping(uint256 => uint256) public minimumFees; 
    // Protocol fee percentage with four decimal places (e.g., 10000 represents 1%)
    uint256 public protocolFeePercentage;
    // Total accumulated protocol fees
    mapping(address => uint256) public totalProtocolFees;
    // Total accumulated bridge fees
    mapping(address => uint256) public totalBridgeFees;
    // Total locked CCFToken balance
    mapping(address => uint256) public lockedBalance;
    //Blockchain which hosts the contract
    uint256 contractBlockchainIndex;
    //Blockchain which hosts the contract
    uint256 protocolFeeDivider;
    // Mapping to keep track of whitelisted tokens
    mapping(address => bool) public tokenWhitelist;
    // Mapping to store minimum fees for each blockchain destination by token address
    mapping(address => mapping(uint256 => uint256)) public minimumFeesToken; 

    // Event emitted when CCFToken is locked
    event Locked(address token, address indexed user, uint256 amount, uint256 protocolFee, uint256 bridgeFee, uint256 blockchainIndex);
    // Event emitted when CCFToken is unlocked
    event Unlocked(address token, address indexed user, uint256 amount, uint256 blockchainIndex);

    // Initialize the contract with the CCFToken token address
    function initialize(uint256 _contractBlockchainIndex) public initializer {
        __Ownable_init(msg.sender);

        updateProtocolFeePercentage(3000); //0.3%
        updateProtocolFeeDivider(1000000);
        updateContractBlockchainIndex(_contractBlockchainIndex);
    }

    // Lock CCF tokens with specified bridge and protocol fees
    function lock(address _token, uint256 amount, uint256 bridgeFee, uint256 blockchainIndex) external {
        require(tokenWhitelist[_token], "Token not whitelisted");
        require(blockchainIndex != contractBlockchainIndex, "Choose another blockchain");
        require(bridgeFee >= minimumFeesToken[_token][blockchainIndex], "Bridge fee is too low");

        uint256 protocolFee = (amount * protocolFeePercentage) / protocolFeeDivider; // Calculate protocol fee
        uint256 totalDeduction = bridgeFee + protocolFee;
        require(amount > totalDeduction, "Insufficient amount after fees");

        require(IERC20Token(_token).transferFrom(msg.sender, address(this), amount), "Transfer failed");
        lockedBalance[_token] += (amount - totalDeduction);
        totalProtocolFees[_token] += protocolFee;
        totalBridgeFees[_token] += bridgeFee;

        emit Locked(_token, msg.sender, amount - totalDeduction, protocolFee, bridgeFee, blockchainIndex);
    }

    // Unlock CCF tokens
    function unlock(address _token, address user, uint256 amount, uint256 blockchainIndex) external onlyOwner {
        require(tokenWhitelist[_token], "Token not whitelisted");
        require(lockedBalance[_token] >= amount, "Insufficient locked balance");
        require(IERC20Token(_token).transfer(user, amount), "Transfer failed");
        lockedBalance[_token] -= amount;

        emit Unlocked(_token, user, amount, blockchainIndex);
    }

    // Update minimum fee for a specific blockchain
    function updateMinimumFee(uint256 blockchainIndex, uint256 fee) public onlyOwner {
        minimumFees[blockchainIndex] = fee;
    }

    // Update minimum fee for a specific blockchain
    function updateMinimumFeeToken(address _token, uint256 blockchainIndex, uint256 fee) public onlyOwner {
        minimumFeesToken[_token][blockchainIndex] = fee;
    }

    // Update minimum fee for a specific blockchain
    function updateMinimumFeeTokenBatch(address _token, uint256[] memory blockchainIndexes, uint256[] memory fees) public onlyOwner {
        require(blockchainIndexes.length == fees.length, "Lengths of arrays do not match");

         for (uint256 i = 0; i < blockchainIndexes.length; i++) {
            updateMinimumFeeToken(_token, blockchainIndexes[i], fees[i]);
        }
    }

    // Update minimum fees by batch
    function updateMinimumFeesBatch(uint256[] memory blockchainIndexes, uint256[] memory fees) external onlyOwner {
        require(blockchainIndexes.length == fees.length, "Lengths of arrays do not match");

        for (uint256 i = 0; i < blockchainIndexes.length; i++) {
            updateMinimumFee(blockchainIndexes[i], fees[i]);
        }
    }

    // Update the protocol fee percentage
    function updateProtocolFeePercentage(uint256 _protocolFeePercentage) public onlyOwner {
        protocolFeePercentage = _protocolFeePercentage;
    }

    // Update the contract blockchainIndex
    function updateContractBlockchainIndex(uint256 _contractBlockchainIndex) public onlyOwner {
        contractBlockchainIndex = _contractBlockchainIndex;
    }

    // Update the protocolFee divider
    function updateProtocolFeeDivider(uint256 _protocolFeeDivider) public onlyOwner {
        protocolFeeDivider = _protocolFeeDivider;
    }
    

    // Withdraw accumulated protocol fees
    function withdrawProtocolFees(address _token, address receiver, uint256 amount) external onlyOwner {
        require(totalProtocolFees[_token] >= amount, "Insufficient protocolFee balance");
        require(IERC20Token(_token).transfer(receiver, amount), "Transfer failed");
        totalProtocolFees[_token] -= amount;
    }

    // Withdraw accumulated bridge fees
    function withdrawBridgeFees(address _token, address receiver, uint256 amount) external onlyOwner {
        require(totalBridgeFees[_token] >= amount, "Insufficient bridgeFee balance");
        require(IERC20Token(_token).transfer(receiver, amount), "Transfer failed");
        totalBridgeFees[_token] -= amount;
    }

    // Function to add a token to the whitelist
    function addToWhitelist(address _token) public onlyOwner {
        tokenWhitelist[_token] = true;
    }

    // Function to remove a token from the whitelist
    function removeFromWhitelist(address _token) public onlyOwner {
        tokenWhitelist[_token] = false;
    }
}